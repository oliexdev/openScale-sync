plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

android {
    compileSdk 36
    defaultConfig {
        applicationId "com.health.openscale.sync"
        minSdkVersion 29
        targetSdkVersion 36
        versionCode 12
        versionName "0.4.2"
        archivesBaseName = "openScale-sync-$versionName"
        multiDexEnabled true
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        compose true
    }

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("../../openScale_sync.keystore")
            def keystoreProperties = new Properties()
            try {
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            } catch (FileNotFoundException e) {
                keystoreProperties = null;
            }

            if (keystoreProperties != null) {
                storeFile file(rootDir.getCanonicalPath() + '/' + keystoreProperties['releaseKeyStore'])
                keyAlias keystoreProperties['releaseKeyAlias']
                keyPassword keystoreProperties['releaseKeyPassword']
                storePassword keystoreProperties['releaseStorePassword']
            }
        }

        oss {
            def keystoreOSSPropertiesFile = rootProject.file("../../openScale_oss_sync.keystore")
            def keystoreOSSProperties = new Properties()
            try {
                keystoreOSSProperties.load(new FileInputStream(keystoreOSSPropertiesFile))
            }
            catch (FileNotFoundException e) {
                keystoreOSSProperties = null;
            }

            if (keystoreOSSProperties != null) {
                storeFile file(rootDir.getCanonicalPath() + '/' + keystoreOSSProperties['releaseKeyStore'])
                keyAlias keystoreOSSProperties['releaseKeyAlias']
                keyPassword keystoreOSSProperties['releaseKeyPassword']
                storePassword keystoreOSSProperties['releaseStorePassword']
            }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        oss {
            applicationIdSuffix ".oss"
            versionNameSuffix "-oss"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.oss
        }
    }

    namespace 'com.health.openscale.sync'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties']
        }
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.9.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.1'
    implementation 'androidx.activity:activity-compose:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.fragment:fragment-ktx:1.8.8'
    implementation 'androidx.navigation:navigation-compose:2.9.0'
    implementation platform('androidx.compose:compose-bom:2025.06.01')
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.runtime:runtime-livedata:1.8.3'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.work:work-runtime-ktx:2.10.2'

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation 'androidx.health.connect:connect-client:1.1.0-rc02'
    implementation 'com.hivemq:hivemq-mqtt-client:1.3.4'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
}
